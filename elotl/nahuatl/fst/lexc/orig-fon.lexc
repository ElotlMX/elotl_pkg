LEXICON Root
Cns ;
Vow ;


LEXICON Loop
Cns ;
Vow ;
Punct ;
 #  ;

LEXICON Vow
FrontVow ;
LowBackVow ;
HighBackVow ;

LEXICON NonConsonantalVow
FrontVow ;
LowBackVow ;
u:u CnsNonGlottal ;
uj:uʔ CnsNonGlottal ;
ú:ú Cns ;
uh:uʔ CnsNonGlottal ;
uh:uʔ # ;
u:u Vow ;

LEXICON FrontVow
i:i Loop ;
i':iʔ Cns ;
í:í Loop ;
ì:iʔ Cns ;
î:iʔ # ;
y:i Cns ;
y:i # ;
e:e Loop ;
e':eʔ Cns ;
é:é Loop ;
ê:eʔ # ;
e%::e%: Loop;
i%::i%: Loop;
ee:e%: Loop;
ii:i%: Loop;

LEXICON HighBackVow
u:u CnsNonGlottal ;
uj:u CnsNonGlottal ;
ú:ú Cns ;
uh:w CnsNonGlottal ;
uh:w # ;
u:w Vow ;

LEXICON LowBackVow
a:a Loop ;
a':aʔ Cns ;
á:á Loop ;
à:aʔ Cns ;
o:o Loop ;
o':oʔ Cns ;
ó:ó Loop ;
a%::a%: Loop;
o%::o%: Loop ;
aa:a%: Loop;
oo:o%: Loop ;

LEXICON Lateral
l:l Loop ;

LEXICON CnsNonLateral
h:ʔ Cns ;
h:ʔ # ;
h:j Vow ;
j:ʔ Cns ;
j:ʔ # ;
j:j Vow ;
b:b Loop ;
c:s FrontVow ;
c:k LowBackVow ;
cuh:kʷ Cns ;
cuh:kʷ # ;
cu:kʷ Vow ;
cu:kʷ Vow ;
chu:tʃu Cns ;
ch:tʃ  FrontVow;
ch:tʃ  LowBackVow;
ch:tʃ  #;
ch:tʃ  Cns;
c:k CnsNonGlottal ;
c:k # ;
cu:ku Cns ; ! a veces esto se debería de realizar como kʷ
ç:s Vow ;
č:t͡ʃ Loop ;
d:d Loop ;
f:f Loop ;
g:g Loop ;
hu:w Vow  "weight: 2.0";
k:k FrontVow ;
k:k LowBackVow ;
k:k Cns ;
ku:ku Cns ;
kw:kʷ Vow ;
ku:kʷ Vow ;
m:m Loop ;
n:n Loop ;
ñ:ñ Vow ;
p:p Loop ;
qu:kʷ LowBackVow ;
qu:k FrontVow ;
r:r Loop ;
s:s Loop ;
š:ʃ Loop ;
ŝ:ʃ Loop ;
t:t Loop ; !CnsNonLateral ;
t:t Vow ;
t:t # ;
tl:ƛ Loop "weight: 2.0" ;
!tl:ƛ # ;
v:v Loop ;
w:w Loop ;
x:ʃ Loop ;
z:s Loop ;
y:y NonConsonantalVow ;

LEXICON CnsNonGlottal
b:b Loop ;
c:s FrontVow ;
c:k LowBackVow ;
cu:kʷ Vow "weight: 2.0";
c:k Vow ;
c:k CnsNonGlottal ;
c:k # ;
ç:s Vow ;
č:tʃ Loop ;
ch:tʃ FrontVow ;
ch:tʃ LowBackVow ;
ch:tʃ CnsNonGlottal ;
ch:tʃ # ;
d:d Loop ;
f:f Loop ;
g:g Loop ;
k:k Loop ;
m:m Loop ;
n:n Loop ;
ñ:ñ Vow ;
p:p Loop ;
qu:kʷ LowBackVow ;
qu:k FrontVow ;
qu:k Cns ;
r:r Loop ;
s:s Loop ;
š:ʃ Loop ;
ŝ:ʃ Loop ;
t:t CnsNonLateral ;
t:t Vow ;
t:t # ;
tl:ƛ Loop ;
v:v Loop ;
w:w Loop ;
x:ʃ Loop ;
z:s Loop ;
l:l Loop ;
y:y NonConsonantalVow ;

LEXICON Cns
Lateral ;
CnsNonLateral ;

LEXICON Punct
%":%" Loop ;
%!:%! Loop ;
¡:¡ Loop ;
%#:%# Loop ;
%$:%$ Loop ;
%%:%% Loop ;
%&:%& Loop ;
%(:%( Loop ;
%):%) Loop ;
%*:%* Loop ;
%+:%+ Loop ;
%,:%, Loop ;
%-:%- Loop ;
%.:%. Loop ;
%\:%\ Loop ;
%::%: Loop ;
%;:%; Loop ;
%<:%< Loop ;
%>:%> Loop ;
%=:%= Loop ;
%>:%> Loop ;
%?:? Loop ;
%@:%@ Loop ;
%[:%[ Loop ;
%]:%] Loop ;
%/:%/ Loop ;
%^:%^ Loop ;
%`:%` Loop ;
%{:%{ Loop ;
%}:%} Loop ;
%|:%| Loop ;
%~:%~ Loop ;
%.:%. Loop ;
%¿:%¿ Loop ;